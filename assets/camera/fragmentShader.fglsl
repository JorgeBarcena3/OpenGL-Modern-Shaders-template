#version 330

#define POINT_N 5
#define DIRECTIONAL_N 1

struct DirectionalLight {

    int eneabled;
    vec3 direction;
  
    vec3 color;
    vec3 ambient;
    vec3 diffuse;
    vec3 specular;
    vec3 intensity;

};  

struct PointLight {  

    int eneabled;
    vec3 position;
    
    vec3 color;
    vec3 ambient;
    vec3 diffuse;
    vec3 specular;
    vec3 intensity;
};  

// Arrays que guardan la informacion de las luces
uniform PointLight pointLights[POINT_N];
uniform DirectionalLight directionalLight[DIRECTIONAL_N];

// Variable de salida
out vec4 fragment_color;

// Variable de entradas desde el vertex shader
in vec2 tx_coord;
in vec3 normal_vec;
in vec3 vertex_pos;
in vec3 camera_position;

// Textura de donde se va a obtener el color base
uniform sampler2D diffuse_sampler;

// Declaraciones adelantadas de las funciones para calcular la iluminacion
vec4 CalculateDirectionalLight(DirectionalLight light, vec3 N, vec3 L);  
vec4 CalculatePointLight(PointLight light, vec3 N, vec3 P, vec3 L);  

void main()
{
    // properties
    vec3 N = normalize(normal_vec);
    vec3 L = normalize(camera_position - vertex_pos);

    vec4 finalColor = vec4(0);
    int lights = 0;

    for(int i = 0; i < DIRECTIONAL_N; i++)
    {
        if(directionalLight[i].eneabled == 1)
        {
            finalColor += CalculateDirectionalLight(directionalLight[i], N, L);
            lights += 1;
        }
    }

    for(int i = 0; i < POINT_N; i++)
    {
        if(pointLights[i].eneabled == 1)
        {
            finalColor += CalculatePointLight(pointLights[i], N, vertex_pos, L);
            lights += 1;
        }
    }

    finalColor /= lights;

    vec4 alphaValue = texture(diffuse_sampler, tx_coord);
    fragment_color = vec4(vec3(finalColor), alphaValue.a);
}

vec4 CalculateDirectionalLight(DirectionalLight light, vec3 N, vec3 L)
{
    vec3 lightDir = normalize(-light.direction);
    // diffuse shading
    float diff = max(dot(N, lightDir), 0.0);
    // specular shading
    vec3 reflectDir = reflect(-lightDir, N);
    float spec = pow(max(dot(L, reflectDir), 0.0), 1);

    // combine results
    vec3 ambient  = light.ambient  * light.color;
    vec3 diffuse  = light.diffuse  * diff * vec3(texture(diffuse_sampler, tx_coord));
    vec3 specular = light.specular * spec * light.color;
    return vec4(light.intensity * (ambient + diffuse + specular), 1);
   

}  

vec4 CalculatePointLight(PointLight light, vec3 N, vec3 P, vec3 L)
{
     vec3 lightDir = normalize(light.position - P);
    // diffuse shading
    float diff = max(dot(N, lightDir), 0.0);
    // specular shading
    vec3 reflectDir = reflect(-lightDir, N);
    float spec = max(dot(L, reflectDir), 0.0);
    // attenuation
    float distance    = length(light.position - P);
    float attenuation = 1 / ( (1.09 * distance) + 0.032f * (distance * distance) );
    
    // combine results
    vec3 ambient  = light.ambient  * light.color;
    vec3 diffuse  = light.diffuse  * diff * vec3(texture(diffuse_sampler, tx_coord));
    vec3 specular = light.specular * spec *light.color;

    ambient   *= attenuation;
    diffuse   *= attenuation;
    specular  *= attenuation;

    return  vec4(light.intensity * (ambient + diffuse + specular), 1);
} 